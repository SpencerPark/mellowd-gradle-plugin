buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.4"
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.12.1"
}
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'maven'

group 'cas.cs4tb3'
version '1.0.2'

license {
    include '**/*.groovy'
    header = rootProject.file('LICENSE')
    strictCheck true
}
build.dependsOn licenseFormat

pluginBundle {
    website = 'http://spencerpark.github.io/MellowD/build/docs/docco/'
    vcsUrl = 'https://github.com/SpencerPark/mellowd-gradle-plugin'
    description = 'Integrate MellowD compilation into the build cycle'
    tags = ['mellowd', 'music']

    plugins {
        mellowdPlugin {
            id = 'io.github.spencerpark.mellowd'
            displayName = 'MellowD language support'
        }
    }
}

repositories {
    mavenLocal()
    jcenter()
}

//Define a shade configuration. Dependencies in this configuration
//are available at compile time and are shaded into the jar.
configurations {
    shade
    shade.transitive = false
    compile.extendsFrom(shade)
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    shade group: 'cas.cs4tb3', name: 'MellowD', version: '+'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'junit:junit:4.12'
}

jar {
    //Include all shaded dependencies in the jar
    from configurations.shade
            .collect { it.isDirectory() ? it : zipTree(it) }

}

uploadArchives {
    repositories.mavenLocal()
}
